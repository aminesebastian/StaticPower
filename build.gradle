buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://maven.minecraftforge.net' }	
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

subprojects {
	apply plugin: 'net.minecraftforge.gradle'
	apply plugin: 'eclipse'
	apply plugin: 'maven-publish'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}

	version = "${mc_version}-${mod_version}"
	group = "theking530"
	archivesBaseName = "TheKing530"
	
    minecraft {
		mappings channel: 'official', version: '1.19.2'
		accessTransformer = file("${projectDir}/src/main/resources/META-INF/accesstransformer.cfg")
		runs {
			client {
				workingDirectory project.file('run')

				property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
				property 'forge.logging.console.level', 'debug'
				
				// For Patchuloi
				property 'mixin.env.remapRefMap', 'true'
				property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
				
				mods {
					mod {
						source sourceSets.main
					}
				}
			}

			server {
				workingDirectory project.file('run')

				property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
				property 'forge.logging.console.level', 'debug'
				
				// For Patchuloi
				property 'mixin.env.remapRefMap', 'true'
				property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
				
				mods {
					mod {
						source sourceSets.main
					}
				}
			}

			data {
				workingDirectory project.file('run')

				property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
				property 'forge.logging.console.level', 'debug'

				// For Patchuloi
				property 'mixin.env.remapRefMap', 'true'
				property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

				args '--mod', "${project.name.toString().toLowerCase()}", '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

				mods {
					mod {
						source sourceSets.main
					}
				}
			}
		}
	}
		
	repositories {
		maven {
			name = "Progwml6 maven"
			url = "https://dvs1.progwml6.com/files/maven/"
		}
		maven { 
			name = "Patchouli Maven"
			url = "https://maven.blamejared.com"
		}
		maven {
			url "https://www.cursemaven.com"
			content {
				includeGroup "curse.maven"
			}
		}
		maven {
			url 'https://maven.covers1624.net'
		}
		maven {
			url "https://maven.k-4u.nl"
		}

	}
			
	dependencies {
		minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
		
		compileOnly fg.deobf("mezz.jei:jei-${mc_version}-common-api:${jei_version}")
		compileOnly fg.deobf("mezz.jei:jei-${mc_version}-forge-api:${jei_version}")
		runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}-forge:${jei_version}")
		
		compileOnly fg.deobf("vazkii.patchouli:Patchouli:${mc_version}-${patchouli_version}:api")
		runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${mc_version}-${patchouli_version}")
		
		implementation fg.deobf("curse.maven:jade-324717:${jade_version}")
		implementation fg.deobf("codechicken:CodeChickenLib:${mc_version}-${ccl_version}:universal")
		
		compileOnly fg.deobf("curse.maven:the-one-probe-245211:3965693")
	}
}